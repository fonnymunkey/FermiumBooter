buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
        classpath("gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0")
    }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "${mod_version}"
group = "${mod_id}"
archivesBaseName = '`' + "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
java.withSourcesJar()
java.withJavadocJar()

minecraft {
    mappings channel: mappings_channel, version: mappings_version
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    shadow 'io.github.llamalad7:mixinextras-common:0.4.1'

    shadow 'org.spongepowered:mixin:0.8.7'
    annotationProcessor 'org.spongepowered:mixin:0.8.7:processor'
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    filesMatching('mcmod.info') {
        expand 'mod_id': mod_id, 'mod_name': mod_name, 'version': project.version,
                'mcversion': minecraft_version, 'mod_description': mod_description,
                'mod_author': mod_author
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()

    exclude 'org/spongepowered/asm/launch/MixinLaunchPlugin*'
    exclude 'org/spongepowered/asm/launch/MixinTransformationService*'
    exclude 'org/spongepowered/asm/launch/platform/container/ContainerHandleModLauncherEx*'

    archiveClassifier.set('')
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Version"   : "1",
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePlugin": "${mixin_loader_class}",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        ])
    }

    archiveClassifier.set('THIN')
}

build.dependsOn(shadowJar)
jar.finalizedBy('reobfJar')